plugins {
    id 'com.github.ben-manes.versions' version "${versionsPluginVersion}"
    id 'com.github.spotbugs' version "${spotbugsPluginVersion}"
    id 'com.gtramontina.ghooks.gradle' version "${ghooksPluginVersion}"
    id 'org.owasp.dependencycheck' version "${dependencyCheckPluginVersion}"
    id 'org.sonarqube' version "${sonarqubePluginVersion}"
    id 'jacoco'
    id 'java'
}

allprojects {
    group = 'com.jrmcdonald'
    version = "${version}"
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
        maven {
            url = 'https://maven.pkg.github.com/jrmcdonald/ms-common'
            credentials {
                username = "jrmcdonald"
                password = project.findProperty('GITHUB_TOKEN') ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    if (it.name != 'ms-common-bom') {
        apply plugin: 'com.github.ben-manes.versions'
        apply plugin: 'com.github.spotbugs'
        apply plugin: 'org.owasp.dependencycheck'
        apply plugin: 'java'
        apply plugin: 'java-library'
        apply plugin: 'java-test-fixtures'

        configurations {
            commonBom
            compileOnly.extendsFrom(commonBom)
            annotationProcessor.extendsFrom(commonBom)
            testCompileOnly.extendsFrom(commonBom)
            testAnnotationProcessor.extendsFrom(commonBom)
            implementation.extendsFrom(commonBom)
            testFixturesImplementation.extendsFrom(commonBom)
        }

        dependencies {
            commonBom enforcedPlatform(project(':ms-common-bom'))

            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'

            testCompileOnly 'org.projectlombok:lombok'
            testAnnotationProcessor 'org.projectlombok:lombok'

            implementation 'com.github.spotbugs:spotbugs-annotations'
        }

        java {
            withJavadocJar()
            withSourcesJar()
        }

        compileJava {
            options.compilerArgs << "-Werror"
            options.compilerArgs << "-Xlint:all"
            options.compilerArgs << "-Xlint:-try"
            options.compilerArgs << "-Xlint:-processing"
        }

        jar {
            enabled = true
        }

        test {
            useJUnitPlatform()
        }
    }
}

apply from: "${rootDir}/gradle/dependencies.gradle"
apply from: "${rootDir}/gradle/quality.gradle"