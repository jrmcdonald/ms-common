plugins {
    id 'com.jrmcdonald.ms-gradle-baseline' version "${gradleBaselinePluginVerson}"
    id 'jacoco'
    id 'java'
}

allprojects {
    group = 'com.jrmcdonald'
    version = "${version}"
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
        maven {
            url = 'https://maven.pkg.github.com/jrmcdonald/ms-common'
            credentials {
                username = "jrmcdonald"
                password = project.findProperty('GITHUB_TOKEN') ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }


    if (it.name != 'ms-common-bom') {
        apply plugin: 'java'
        apply plugin: 'java-library'
        apply plugin: 'java-test-fixtures'

        configurations {
            testFixturesCompileOnly.extendsFrom(commonPlatform)
            testFixturesAnnotationProcessor.extendsFrom(commonPlatform)
            testFixturesImplementation.extendsFrom(commonPlatform)
        }

        dependencies {
            commonPlatform enforcedPlatform(project(':ms-common-bom'))

            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'

            testCompileOnly 'org.projectlombok:lombok'
            testAnnotationProcessor 'org.projectlombok:lombok'

            implementation 'com.github.spotbugs:spotbugs-annotations'
        }

        java {
            withJavadocJar()
            withSourcesJar()
        }

        jar {
            enabled = true
        }
    }
}