plugins {
    id 'java-platform'
    id 'maven-publish'
}

javaPlatform {
    allowDependencies()
}

dependencies {
    constraints {
        rootProject.allprojects {
            if (!path.startsWith(":ms-common-bom")) {
                plugins.withId("maven-publish") {
                    afterEvaluate {
                        api "${group}:${name}:${version}"
                    }
                }
            }
        }

        api(externalLibs.biweekly)
        api(externalLibs.jackson.datatype.jsr310)
        api(externalLibs.logstash.logback.encoder)
        api(externalLibs.micrometer.registry.prometheus)
        api(externalLibs.spotbugs.annotations)
        api(externalLibs.springdoc.openapi.kotlin)
        api(externalLibs.springdoc.openapi.ui)
        api(externalLibs.springdoc.openapi.security)
        api(externalLibs.springdoc.openapi.webflux.ui)
        api(externalLibs.swagger.core)

        api(testLibs.beantester)
        api(testLibs.mockk.mockk)
        api(testLibs.mockk.springmockk)
        api(testLibs.squareup.mockwebserver)
        api(testLibs.squareup.okhttp)
        api(testLibs.testcontainers.core)
        api(testLibs.testcontainers.junit.jupiter)
        api(testLibs.testcontainers.postgresql)
    }

    api platform(platformLibs.spring.boot.dependencies)
    api platform(platformLibs.spring.cloud.dependencies)
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/jrmcdonald/ms-common")
            credentials {
                username = "jrmcdonald"
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.javaPlatform)
        }
    }
}